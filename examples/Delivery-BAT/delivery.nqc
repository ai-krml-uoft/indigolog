/***************************************************************************
/*
/* FILE: Delivery/delivery.nqc
/*
/* NQC code for Legolog (complex) delivery example
/*
/* WRITTEN BY: Sebastian Sardina
/* BASED ON PREVIOUS VERSION BY: Maurice Pagnucco and Hector J. Levesque
/* REVISED: May 22, 2001
/* TESTED: nqc 2.1 r1
/*
/***************************************************************************
/*                            May 18, 2001
/*
/* This software was developed by the Cognitive Robotics Group under the
/* direction of Hector Levesque and Ray Reiter.
/*
/*       Do not distribute without permission.
/*       Include this notice in any copy made.
/*
/*
/*        Copyright (c) 2000 by The University of Toronto,
/*                       Toronto, Ontario, Canada.
/*
/*                         All Rights Reserved
/*
/* Permission to use, copy, and modify, this software and its
/* documentation for non-commercial research purpose is hereby granted
/* without fee, provided that the above copyright notice appears in all
/* copies and that both the copyright notice and this permission notice
/* appear in supporting documentation, and that the name of The University
/* of Toronto not be used in advertising or publicity pertaining to
/* distribution of the software without specific, written prior
/* permission.  The University of Toronto makes no representations about
/* the suitability of this software for any purpose.  It is provided "as
/* is" without express or implied warranty.
/*
/* THE UNIVERSITY OF TORONTO DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS
/* SOFTWARE, INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND
/* FITNESS, IN NO EVENT SHALL THE UNIVERSITY OF TORONTO BE LIABLE FOR ANY
/* SPECIAL, INDIRECT OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER
/* RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF
/* CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN
/* CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
/*
/***************************************************************************
/*
/* This file provides application dependent code:
/* -- void initialize(): perform any necessary initialization of RCX
/*      e.g., define input/output types, display sensor values,
/*      start exogenous action monitors, etc.
/* -- void startBehaviour(int behNum): start action corresponding to behNum.
/*      This number must correspond to that in an application dependent
/*      Golog file (e.g., delivery.pl) defined using predicate actionNum/2
/* -- void stopAllBehaviours():terminate all currently running behaviour tasks.
/*      Can reset variables here too
/* -- void panicAction(): action to take when RCX status becomes PANIC i.e.,
/*      when Prolog is not accepting transmissions
/* Plus tasks, functions, subroutines for the behaviours.
/*
/***************************************************************************/

/***************************************************************************/
/* INCLUDES
/***************************************************************************/

#include "delivery.nqh"
#include "../../Env/control.nqh"
//#include "../../Env/control-debug.nqh" 

/***************************************************************************/
/* CONSTANTS
/***************************************************************************/

// Behaviour message map: should correspond to Prolog actionNo(+Action, -Num)
// predicate

// Primitive Actions
#define VOID_ACTION             0
#define TURN_AROUND             1
#define TURN_LEFT               2
#define TURN_RIGHT              3
#define PICK_UP                 4
#define DROP_OFF                5
#define START_TO_NEXT_STATION   6
#define MOVE_BACK               7
#define MOVE_FWD                8
#define FREEZE                  9
#define SENSE_DOOR              10
#define RESET_ROBOT             11
#define ACTIVATE_ALARM          12
#define SENSE_LINE              13
#define SEARCH_PATH             14

// Exogenous Actions
#define ARRIVE_AT_STATION       20
#define STOP_ABNORMALLY         21
#define PUSH_GO_BUTTON          22
#define GET_STUCK               23

// Sensors and motors
#define GO_BUTTON               SENSOR_1
#define BLOCK_SENSOR            SENSOR_2
#define PATH_SENSOR             SENSOR_3

#define LEFT_MOTOR              OUT_C
#define RIGHT_MOTOR             OUT_A
#define DUMPER                  OUT_B

// Light sensor thresholds
#define BLOCK_THRESHOLD_L       20 // Below this for a blocked path
#define BLOCK_THRESHOLD_H       40 // Above this for a blocked path
#define LINE_THRESHOLD          42 // Below this for black line
#define STOPPER_THRESHOLD       54 // Above this for way station

// Other constants
#define TURN_SPEED              6  // Needs to be a bit higher than move speed to turn
#define MOVE_SPEED              2  // Normal forward speed
#define DUMPER_SPEED            1  // Speed for dumper motor
#define DUMPER_TIME             50 // Time dumper motor is engaged
#define INITIAL_TURN_TIME       3  // Time for first "sweep" when locating line
#define TURN_TIME               40 // Time to complete one revolution (roughly)
#define SMALL_WAIT              40 // Small time to wait
#define MOVE_WAIT               40 // Time to wait before line tracking
#define RCX_PYRAMID_DELAY       30 // Time it is safe to wait before Prolog
                                   //   will time out

/***************************************************************************/
/* VARIABLES
/***************************************************************************/

// initialize: Anything that needs to be set up when RCX is started
void initialize()
{
    // Initialize sensors
    SetSensor(PATH_SENSOR, SENSOR_LIGHT);
    SetSensor(BLOCK_SENSOR, SENSOR_LIGHT);
    SetSensor(GO_BUTTON, SENSOR_TOUCH);

    // Display value of light sensor (not essential)
    SelectDisplay(DISPLAY_SENSOR_2);

    // Initialize exogenous action monitors
    start monitorPushButton;
}

// panicAction: What to do when we get in a PANIC state
void panicAction()
{
    PlayTone(1000, 5);
    stopAllBehaviours();
    Off(RIGHT_MOTOR + LEFT_MOTOR);     // Stop motors
}

// stopAllBehaviours: Stop behaviours
void stopAllBehaviours()
{
    stop goToNextStation;

    Off(RIGHT_MOTOR + LEFT_MOTOR);     // Stop motors

    // Reset variables
    exogAction = NO_EXOG_ACTION;
}

// startBehaviour: Start a behaviour associated with action BehNum.
//     Need one case for each primitive action
void startBehaviour(int behNum)
{
    if (behNum == TURN_AROUND)
        turnAround(1);
    else if (behNum == TURN_LEFT)
        turnLeft();
    else if (behNum == TURN_RIGHT)
        turnRight();
    else if (behNum == PICK_UP)
        pickUp();
    else if (behNum == DROP_OFF)
        dropOff();
    else if (behNum == START_TO_NEXT_STATION)
        startToNextStation();
    else if (behNum == MOVE_BACK)
        moveLittle(-1);           // Move back a little
    else if (behNum == MOVE_FWD)
        moveLittle(1);	          // Move forward a little
    else if (behNum == ACTIVATE_ALARM)
        activateAlarm();
    else if (behNum == FREEZE)
        freeze();
    else if (behNum == SENSE_DOOR)
        senseDoor();
    else if (behNum == SENSE_LINE)
        senseLine();
    else if (behNum == SEARCH_PATH)
        searchPath();
    else if (behNum == RESET_ROBOT)
        stopAllBehaviours();
    else if (behNum == VOID_ACTION)
	sensorVal = 0;
}

// senseDoor: Sense if the door in front is open
void senseDoor()
{
   sensorVal = BLOCK_SENSOR;
}

// senseLine: Sense the line sensor
void senseLine()
{
   sensorVal = PATH_SENSOR;
}

// pickUp: Robot picks up the mail 
void pickUp()
{
    SetPower(DUMPER, DUMPER_SPEED);
    OnFwd(DUMPER);
    Wait(DUMPER_TIME);
    Off(DUMPER);

    sensorVal = 0; // This is not a sensing action; return 0
}

// dropOff: Robot drops off the mail it has 
void dropOff()
{
    SetPower(DUMPER, DUMPER_SPEED);
    OnRev(DUMPER);
    Wait(DUMPER_TIME);
    Off(DUMPER);

    sensorVal = 0; // This is not a sensing action; return 0
}

// turnAround: Turn the robot 180 degrees (roughly)
void turnAround(int side)
{
    int once = 1;

    ClearTimer(1);
    SetPower(LEFT_MOTOR + RIGHT_MOTOR, TURN_SPEED);
    if (side==1) {  		// Turn to the right
 	    Fwd(LEFT_MOTOR);
    	    Rev(RIGHT_MOTOR);
    } else {			// Turn to the left
	    Rev(LEFT_MOTOR);
    	    Fwd(RIGHT_MOTOR);
    }	
    On(LEFT_MOTOR + RIGHT_MOTOR);
    Wait(MOVE_WAIT); // Move off line
    while (Timer(1) < TURN_TIME && PATH_SENSOR > LINE_THRESHOLD) {
        if (Timer(1) > RCX_PYRAMID_DELAY && once == 1) { // Ask for more time -
            SendMessage(RCX_DELAY_MESSAGE);               //   once only
            once = 0;
        }
    }
    Off(LEFT_MOTOR + RIGHT_MOTOR);

    sensorVal = 0;
}


void turnLeft2()
{
    moveLittle(1);
    turnAround(0);

    sensorVal = 0;
}

void turnLeft()
{
    int once = 1;

    SetPower(LEFT_MOTOR, MOVE_SPEED);
    SetPower(RIGHT_MOTOR, TURN_SPEED);
    Rev(LEFT_MOTOR);
    Fwd(RIGHT_MOTOR);
    On(LEFT_MOTOR + RIGHT_MOTOR);
    Wait(50);
    ClearTimer(1);
    while (Timer(1) < TURN_TIME && PATH_SENSOR > LINE_THRESHOLD) {
        if (Timer(1) > RCX_PYRAMID_DELAY && once == 1) { // Ask for more time -
            SendMessage(RCX_DELAY_MESSAGE);               //   once only
            once = 0;
       }
    }
    Off(LEFT_MOTOR + RIGHT_MOTOR);

//    direction = 1;
    sensorVal = 0;
}

void turnRight()
{
    moveLittle(1);
    turnAround(1);

    start goToNextStation;
    Wait(150);                // Wait for a bit while moving
    stop goToNextStation;


    start goToBackStation;

    sensorVal = 0;
}


// turnRight: Turn robot into right branch
void turnRight2()
{
    int once = 1;

    SetPower(RIGHT_MOTOR, MOVE_SPEED);
    SetPower(LEFT_MOTOR, TURN_SPEED);
    Rev(RIGHT_MOTOR);
    Fwd(LEFT_MOTOR);
    On(LEFT_MOTOR + RIGHT_MOTOR);
    Wait(50);
    ClearTimer(1);
    while (Timer(1) < TURN_TIME && PATH_SENSOR > LINE_THRESHOLD) {
        if (Timer(1) > RCX_PYRAMID_DELAY && once == 1) { // Ask for more time -
            SendMessage(RCX_DELAY_MESSAGE);               //   once only
            once = 0;
        }
    }
    Off(LEFT_MOTOR + RIGHT_MOTOR);

//    direction = 0;
    sensorVal = 0;
}


// moveLittle: Move the robot forward/backwards a little bit
void moveLittle(int direction)
{
    SetPower(LEFT_MOTOR + RIGHT_MOTOR, MOVE_SPEED);
    if (direction==1) {
	    Fwd(LEFT_MOTOR);
	    Fwd(RIGHT_MOTOR);
    } else {
	    Rev(LEFT_MOTOR);
	    Rev(RIGHT_MOTOR);
    }
    On(LEFT_MOTOR + RIGHT_MOTOR);
    Wait(SMALL_WAIT);                // Wait for a bit while moving
    Off(LEFT_MOTOR + RIGHT_MOTOR);
}

// searchPath: Move randomly until we hit a line
void searchPath()
{
    SetPower(LEFT_MOTOR + RIGHT_MOTOR, MOVE_SPEED);
}


// startToNextStation: Start moving towards the next way station
void startToNextStation()
{
    start goToNextStation;

    sensorVal = 0;
}

// goToNextStation: Line following behaviour
//     Some ideas in this code are based on Ch 8 of "Dave Baum's Definitive
//     Guide to LEGO MINDSTORMS", APress 2000. http://www.apress.com/Mindstorms/
task goToNextStation()
{
    int direction = 1, time = INITIAL_TURN_TIME, firstLoop = 1, done = 0;

    ClearTimer(0);

    // Start moving - move clear of landmark
    SetPower(LEFT_MOTOR + RIGHT_MOTOR, MOVE_SPEED);
    OnFwd(LEFT_MOTOR + RIGHT_MOTOR);
    Wait(MOVE_WAIT);

    while(done != 1) {
        if (BLOCK_SENSOR < BLOCK_THRESHOLD_L) { // The path is blocked
            Off(RIGHT_MOTOR + LEFT_MOTOR);
            exogAction = GET_STUCK;
            done = 1;
	    PlaySound(SOUND_DOWN);
        } else if (PATH_SENSOR < LINE_THRESHOLD) { // On the line
            ClearTimer(0);
            time = INITIAL_TURN_TIME;
            firstLoop = 1;
            SetPower(RIGHT_MOTOR + LEFT_MOTOR, MOVE_SPEED);
            Fwd(RIGHT_MOTOR + LEFT_MOTOR);
        } else if (PATH_SENSOR > STOPPER_THRESHOLD) { // Found a stopper
            Off(RIGHT_MOTOR + LEFT_MOTOR);
            exogAction = ARRIVE_AT_STATION;
            done = 1;
        } else if (Timer(0) > time || firstLoop == 1) { // Find line again
            if (firstLoop != 1) { // Do this just after we come off the line
                direction *= -1;
                time *= 3;
                if (Timer(0) > TURN_TIME) {  // RCX is lost
                    done = 1;
                    exogAction = STOP_ABNORMALLY;
		    PlaySound(SOUND_DOWN);
                }
            }
            firstLoop = 0;
            SetPower(RIGHT_MOTOR + LEFT_MOTOR, TURN_SPEED);
            if (direction == 1) {
                Rev(LEFT_MOTOR);
                Fwd(RIGHT_MOTOR);
            } else {
                Fwd(LEFT_MOTOR);
                Rev(RIGHT_MOTOR);
            }
        }
    }
}


// goToBackStation: Line following behaviour
//     Some ideas in this code are based on Ch 8 of "Dave Baum's Definitive
//     Guide to LEGO MINDSTORMS", APress 2000. http://www.apress.com/Mindstorms/
task goToBackStation()
{
    int direction = 1, time = INITIAL_TURN_TIME, firstLoop = 1, done = 0;

    ClearTimer(0);

    // Start moving - move clear of landmark
    SetPower(LEFT_MOTOR + RIGHT_MOTOR, MOVE_SPEED);
    OnRev(LEFT_MOTOR + RIGHT_MOTOR);
    Wait(MOVE_WAIT);

    while(done != 1) {
        if (BLOCK_SENSOR < BLOCK_THRESHOLD_L) { // The path is blocked
            Off(RIGHT_MOTOR + LEFT_MOTOR);
            exogAction = GET_STUCK;
            done = 1;
	    PlaySound(SOUND_DOWN);
        } else if (PATH_SENSOR < LINE_THRESHOLD) { // On the line
            ClearTimer(0);
            time = INITIAL_TURN_TIME;
            firstLoop = 1;
            SetPower(RIGHT_MOTOR + LEFT_MOTOR, MOVE_SPEED);
            Rev(RIGHT_MOTOR + LEFT_MOTOR);
        } else if (PATH_SENSOR > STOPPER_THRESHOLD) { // Found a stopper
            Off(RIGHT_MOTOR + LEFT_MOTOR);
            exogAction = ARRIVE_AT_STATION;
            done = 1;
        } else if (Timer(0) > time || firstLoop == 1) { // Find line again
            if (firstLoop != 1) { // Do this just after we come off the line
                direction *= -1;
                time *= 3;
                if (Timer(0) > TURN_TIME) {  // RCX is lost
                    done = 1;
                    exogAction = STOP_ABNORMALLY;
		    PlaySound(SOUND_DOWN);
                }
            }
            firstLoop = 0;
            SetPower(RIGHT_MOTOR + LEFT_MOTOR, TURN_SPEED);
            if (direction != 1) {
                Rev(LEFT_MOTOR);
                Fwd(RIGHT_MOTOR);
            } else {
                Fwd(LEFT_MOTOR);
                Rev(RIGHT_MOTOR);
            }
        }
    }
}


// ActivateAlarm: sets the alarm on
void activateAlarm()
{
    PlayTone(500, 5);

    sensorVal = 0; // This is not a sensing action; return 0
}

// Freeze: stop going to next station
void freeze()
{
    stop goToNextStation;
    Off(RIGHT_MOTOR + LEFT_MOTOR);

    sensorVal = 0; // This is not a sensing action; return 0
}


/***************************************************************************
/* EXOGENOUS MONITORING
/***************************************************************************/

// monitorPushButton: Monitor status of pushbutton - it acts as a "go" button
task monitorPushButton()
{
    while(true)
        if (GO_BUTTON == 1 ) {
            PlaySound(SOUND_UP);
            exogAction = PUSH_GO_BUTTON;
//            OnFwd(DUMPER);
//            Wait(DUMPER_TIME);
//            Float(DUMPER);
            ClearSensor(GO_BUTTON);
        }
}

/***************************************************************************
/* EOF: Delivery/delivery.nqc
/***************************************************************************/
